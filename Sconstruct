env = Environment(
    PION_HOME='dependencies/pion-net-3.0.17'
)

env.Append(
    CPPPATH=[
        'include', 
        'dependencies/jsoncpp-src-0.5.0/include',
        env.subst('$PION_HOME/net/include'), 
        env.subst('$PION_HOME/common/include'),
    ], 
    LIBPATH=[
        'dependencies/jsoncpp-src-0.5.0/libs',
        env.subst('$PION_HOME'),
    ],
    LINKFLAGS=['-pthread'],
    LIBS=[
        'dl',
        'z',
        'bz2',
        'log4cplus',
        'boost_thread-mt',
        'boost_system-mt',
        'boost_filesystem-mt',
        'boost_regex-mt',
        'boost_date_time-mt',
        'boost_signals-mt',
        'boost_iostreams-mt',
        'boost_program_options-mt',
        'pion-net',
        'pion-common',
        'json_libmt'
    ]
)

SConscript([env.subst('$PION_HOME/Sconstruct')])

# parse configuration for yaml-cpp
env.ParseConfig('pkg-config --cflags --libs yaml-cpp')

opt = env.Clone()
opt.Append(CCFLAGS=['-pthread', '-O3', '-Wall', '-DNDEBUG'])
opt.VariantDir('opt', 'src', duplicate=0)
opt_objs = opt.Glob('opt/*.cpp')
lite_comet = opt.Program('lite_comet', opt_objs)

dbg = env.Clone()
dbg.Append(CCFLAGS=['-pthread', '-Wall', '-g', '-O0', '-DDEBUG', '-D_DEBUG'])
dbg.VariantDir('dbg', 'src', duplicate=0)
dbg_objs = dbg.Glob('dbg/*.cpp')
dbg_lite_comet = dbg.Program('dbg_lite_comet', dbg_objs)
